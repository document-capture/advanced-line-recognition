OBJECT Codeunit 61000 Adv. Line Recognition Mgt.
{
  OBJECT-PROPERTIES
  {
    Date=13.06.19;
    Time=00:00:00;
    Version List=ALR007;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      Text001@1000000000 : TextConst 'DEU=Zuweisung abgebrochen, da kein Feld fr die Verknpfung ausgew„hlt worden sind!;ENU=Linking cancelled because you didn''t select fields for linking!';
      Text002@1000000001 : TextConst 'DEU=Das Feld "%1" ist nun mit dem Ankerfeld "%2" verknpft!;ENU=%1 fields have been linked to the line identification field!';
      Text003@1000000002 : TextConst 'DEU=Das Ankerfeld %1 ist noch nicht trainiert!;ENU=The line identification %1 field is not trained yet!';
      Text004@1000000003 : TextConst 'DEU=Bitte den Beispielwert fr %1 (%2) in der ersten Belegzeile trainieren!;ENU=Please train the example value for %1 in the first document line!';
      Text005@1000000004 : TextConst 'DEU=Bitte w„hlen Sie zuerst das Ankerfeld aus!;ENU=Please choose the line identification field first!';
      Text006@1000000005 : TextConst 'DEU=Codeunit %1 existiert nicht!;ENU=Codeunit %1 does not exist!';
      Text007@1113400000 : TextConst 'DEU=Es wurden %1 Felder fr die Verarbeitung von Captions in der Zeile eingerichtet.;ENU=%1 fields have been linked to the line identification field!';
      Text008@1113400001 : TextConst 'DEU=Fehler! Fr Feld %1 wurde noch keine Caption hinterlegt! Bitte pflegen Sie die Caption zuerst.;ENU=Error! For the field %1 hasn|t been trained a caption! Please train the caption first.';
      Text009@1113400002 : TextConst 'DEU=Das Feld "%1" ist als versetztes Feld mit šberschrift konfiguriert.;ENU=The value for field "%1" will be searched by the column heading now.';
      Text011@1113400004 : TextConst 'DEU=Bitte w„hlen Sie zuerst das Feld aus, fr welches Sie das Ersatzfeld einrichten m”chten.;ENU=Please choose the line identification field first!';
      Text013@1113400006 : TextConst 'DEU=W„hlen Sie nun das Feld aus, welches Sie mit dem Ankerfeld verknpfen m”chten.;ENU=Choose the field, which should be linked with the line identifiying field.';
      Text014@1113400007 : TextConst 'DEU=Fehler bei Zuweisung.;ENU=Error during assigning the field';
      Text015@1113400008 : TextConst 'DEU=Bitte w„hlen Sie das Feld aus, welches ber die šberschrift gefunden werden soll.;ENU=Select the field that should be found by the column heading.';
      Text016@1113400009 : TextConst 'DEU="Bitte w„hlen Sie das Feld aus, welches innerhalb der Position ber die Caption gefunden werden soll. ";ENU=Choose the field whose value should be found by the caption in the current line.';
      Text017@1113400010 : TextConst 'DEU="Das Feld ""%1"" wird nun innerhalb der Position ber die Caption gesucht. ";ENU=The value of field "%1" will now been searched via the caption in the current line.';
      Text018@1113400011 : TextConst 'DEU=Bitte w„hlen Sie das Feld aus, welches als Gruppenfeld konfiguriert werden soll.;ENU=Choose the field, which should be configured as group field.';
      Text019@1113400012 : TextConst 'DEU=Das Feld "%1" ist nun als Gruppenfeld eingerichtet.;ENU="The field ""%1"" is now configured as group field. "';
      Text020@1000000006 : TextConst 'DEU=Es gibt noch kein trainiertes Feld fr die erste Position! Bitte trainieren Sie erst ein Feld in der Position.;ENU=There is no trained field for the first position yet! Please train one field in the position first.';

    PROCEDURE SetToAnchorLinkedField@1000000010(VAR TempDocumentLine@1000000008 : Record 6085596);
    VAR
      AnchorField@1000000007 : Record 6085580;
      LinkedField@1000000005 : Record 6085580;
      AnchorFieldDocumentValue@1113400000 : Record 6085593;
      LinkedFieldDocumentValue@1000000001 : Record 6085593;
      FieldsLinked@1000000000 : Integer;
    BEGIN
      // Get the anchor field that defines the position
      MESSAGE(Text005);
      SelectField(AnchorField,TempDocumentLine."Template No.",'');

      // Get document value of the anchor field => is mandatory
      AnchorFieldDocumentValue.SETRANGE("Document No.",TempDocumentLine."Document No.");
      AnchorFieldDocumentValue.SETRANGE("Is Value",TRUE);
      AnchorFieldDocumentValue.SETRANGE(Code,AnchorField.Code);
      AnchorFieldDocumentValue.SETRANGE("Line No.",1);
      AnchorFieldDocumentValue.SETRANGE(Type,AnchorFieldDocumentValue.Type::Line);
      AnchorFieldDocumentValue.SETRANGE("Is Valid",TRUE);
      AnchorFieldDocumentValue.SETRANGE("Template No.",TempDocumentLine."Template No.");
      IF NOT AnchorFieldDocumentValue.FINDFIRST THEN
        ERROR(Text003,AnchorField.Code);

      // Select the field that should be linked with anchor field
      MESSAGE(Text013);
      IF NOT SelectField(LinkedField,TempDocumentLine."Template No.",AnchorField.Code) THEN
        ERROR(Text001);

      // Link the selected field to anchor field
      // Find the value of the selected field
      LinkedFieldDocumentValue.SETRANGE("Document No.",TempDocumentLine."Document No.");
      LinkedFieldDocumentValue.SETRANGE("Is Value",TRUE);
      LinkedFieldDocumentValue.SETRANGE(Code,LinkedField.Code);
      LinkedFieldDocumentValue.SETRANGE("Line No.",0,1);
      LinkedFieldDocumentValue.SETRANGE(Type,LinkedFieldDocumentValue.Type::Line);
      LinkedFieldDocumentValue.SETRANGE("Is Valid",TRUE);
      LinkedFieldDocumentValue.SETRANGE("Template No.",TempDocumentLine."Template No.");
      IF NOT LinkedFieldDocumentValue.FINDFIRST THEN
        ERROR(Text004,LinkedField."Field Name",LinkedField.Code);  //value is mandatory

      ResetField(LinkedField);

      // Calculate and save the offset values at the linked field
      LinkedField."Offset Top" := LinkedFieldDocumentValue.Top - AnchorFieldDocumentValue.Top;
      LinkedField."Offset Left" := LinkedFieldDocumentValue.Left - AnchorFieldDocumentValue.Left;
      LinkedField."Offset Bottom" := LinkedFieldDocumentValue.Bottom - LinkedFieldDocumentValue.Top;
      LinkedField."Offset Right" := LinkedFieldDocumentValue.Right - LinkedFieldDocumentValue.Left;
      LinkedField."Advanced Line Recognition Type" := LinkedField."Advanced Line Recognition Type"::LinkedToAnchorField;
      LinkedField."Anchor Field" := AnchorFieldDocumentValue.Code;

      UpdateExecutionSequence(LinkedField,LinkedField."Anchor Field");

      IF LinkedField.MODIFY(TRUE) THEN BEGIN
        SetTemplateToALRProcessing(TempDocumentLine."Template No.");
        MESSAGE(Text002,LinkedField."Field Name",AnchorField."Field Name");
      END ELSE
        MESSAGE(Text014);

    END;

    PROCEDURE SetToFieldSearchWithColumnHeding@1113400003(VAR TempDocumentLine@1113400002 : Record 6085596);
    VAR
      LineIdentFieldDocumentValue@1000000001 : Record 6085593;
      SelectedField@1113400001 : Record 6085580;
      SelectedFieldDocumentValue@1000000004 : Record 6085593;
      TemplateFieldCaption@1000000000 : Record 6085581;
    BEGIN
      // Find field value of a normal position field
      GetLineIdentifierValue(LineIdentFieldDocumentValue, TempDocumentLine."Document No.", TempDocumentLine."Template No.");

      // Select field
      MESSAGE(Text015);
      IF NOT SelectField(SelectedField,TempDocumentLine."Template No.",'') THEN
        ERROR(Text001);

      // Check that the selected field has at least one caption
      TemplateFieldCaption.SETRANGE("Template No.",SelectedField."Template No.");
      TemplateFieldCaption.SETRANGE(Code,SelectedField.Code);
      TemplateFieldCaption.SETRANGE(Type,TemplateFieldCaption.Type::Line);
      IF TemplateFieldCaption.ISEMPTY THEN
        ERROR(Text008, SelectedField.Code);

      // Find the value of the selected field
      GetSelectedFieldValue(SelectedFieldDocumentValue,SelectedField,TempDocumentLine."Document No.",TempDocumentLine."Template No.");

      ResetField(SelectedField);

      // Setup field for column heading search
      SelectedField."Advanced Line Recognition Type" := SelectedField."Advanced Line Recognition Type"::FindFieldByColumnHeading;

      IF SelectedFieldDocumentValue.Top < LineIdentFieldDocumentValue.Top THEN
        SelectedField."Field Position" := SelectedField."Field Position"::AboveAnchor
      ELSE
        SelectedField."Field Position" := SelectedField."Field Position"::BelowAnchor;

      IF SelectedField.MODIFY(TRUE) THEN BEGIN
        SetTemplateToALRProcessing(TempDocumentLine."Template No.");
        MESSAGE(Text009, SelectedField."Field Name");
      END ELSE
        MESSAGE(Text014);
    END;

    PROCEDURE SetToFieldSearchWithCaption@1113400000(VAR TempDocumentLine@1113400000 : Record 6085596);
    VAR
      LineIdentFieldDocumentValue@1000000001 : Record 6085593;
      SelectedField@1113400001 : Record 6085580;
      SelectedFieldDocumentValue@1000000002 : Record 6085593;
      TemplateFieldCaption@1000000000 : Record 6085581;
    BEGIN
      // Find field value of a normal position field
      GetLineIdentifierValue(LineIdentFieldDocumentValue, TempDocumentLine."Document No.", TempDocumentLine."Template No.");

      // Select field
      MESSAGE(Text016);
      IF NOT SelectField(SelectedField,TempDocumentLine."Template No.",'') THEN
        ERROR(Text001);

      // Check that the selected field has at least one caption
      TemplateFieldCaption.SETRANGE("Template No.",SelectedField."Template No.");
      TemplateFieldCaption.SETRANGE(Code,SelectedField.Code);
      TemplateFieldCaption.SETRANGE(Type,TemplateFieldCaption.Type::Line);
      IF TemplateFieldCaption.ISEMPTY THEN
        ERROR(Text008,SelectedField.Code);

      // Find the value of the selected field
      GetSelectedFieldValue(SelectedFieldDocumentValue,SelectedField,TempDocumentLine."Document No.",TempDocumentLine."Template No.");

      ResetField(SelectedField);

      // Setup field for field search by caption
      SelectedField."Advanced Line Recognition Type" := SelectedField."Advanced Line Recognition Type"::FindFieldByCaptionInPosition;

      IF SelectedFieldDocumentValue.Top < LineIdentFieldDocumentValue.Top THEN
        SelectedField."Field Position" := SelectedField."Field Position"::AboveAnchor
      ELSE
        SelectedField."Field Position" := SelectedField."Field Position"::BelowAnchor;

      IF SelectedField.MODIFY(TRUE) THEN BEGIN;
        SetTemplateToALRProcessing(TempDocumentLine."Template No.");
        MESSAGE(Text017,SelectedField."Field Name");
      END ELSE
        MESSAGE(Text014);
    END;

    PROCEDURE ResetFieldFromMenu@1(VAR TempDocumentLine@1001 : Record 6085596);
    VAR
      FieldToReset@1000 : Record 6085580;
    BEGIN
      IF SelectField(FieldToReset,TempDocumentLine."Template No.",'') THEN BEGIN
        ResetField(FieldToReset);
        FieldToReset.MODIFY(TRUE);
      END;
    END;

    LOCAL PROCEDURE ResetField@1000000004(VAR TemplateField@1000000000 : Record 6085580);
    BEGIN
      // Reset the current field to default values
      WITH TemplateField DO BEGIN
        "Search for Value" := FALSE;
        Required := FALSE;
        CLEAR("Advanced Line Recognition Type");
        CLEAR("Anchor Field");
        CLEAR("Offset Top");
        CLEAR("Offset Bottom");
        CLEAR("Offset Left");
        CLEAR("Offset Right");
        CLEAR("Field Position");
        CLEAR(Sorting);
        CLEAR("Substitution Field");
        CLEAR("Get Value from Previous Value");
      END;
    END;

    LOCAL PROCEDURE SelectField@1113400002(VAR TemplateField@1000000000 : Record 6085580;TemplateNo@1113400001 : Code[20];ExcludedFieldsFilter@1113400000 : Text[250]) : Boolean;
    VAR
      lTemplateFieldList@1000000003 : Page 6085602;
    BEGIN
      WITH TemplateField DO BEGIN
        SETRANGE("Template No.",TemplateNo);
        SETRANGE(Type,Type::Line);
        IF ExcludedFieldsFilter <> '' THEN
          SETFILTER(Code,'<>%1',ExcludedFieldsFilter);
        lTemplateFieldList.SETTABLEVIEW(TemplateField);
        lTemplateFieldList.LOOKUPMODE(TRUE);
        IF lTemplateFieldList.RUNMODAL = ACTION::LookupOK THEN BEGIN
          lTemplateFieldList.GETRECORD(TemplateField);
          EXIT(TRUE);
        END;
      END;
    END;

    PROCEDURE SetTemplateToALRProcessing@1113400005(TemplateNo@1113400000 : Code[20]);
    VAR
      lTemplate@1113400001 : Record 6085579;
    BEGIN
      // Change Codeunit ID to the advanced line recognition codeunit on template
      IF lTemplate.GET(TemplateNo) THEN BEGIN
        lTemplate.VALIDATE("Codeunit ID: Line Capture",GetAdvLineRecCodeunit());
        lTemplate.MODIFY(TRUE);
      END;
    END;

    LOCAL PROCEDURE GetAdvLineRecCodeunit@1000000000() : Integer;
    VAR
      Object@1000000000 : Record 2000000001;
    BEGIN
      Object.SETRANGE(Type,Object.Type::Codeunit);
      Object.SETRANGE(Name,'Adv. Purch/Sales Line Capture');
      IF Object.ISEMPTY THEN
        ERROR(Text006,'Adv. Purch/Sales Line Capture');

      IF Object.FINDSET THEN
        EXIT(Object.ID);
    END;

    LOCAL PROCEDURE GetLineIdentifierValue@1000000002(VAR LineIdentFieldDocumentValue@1000000000 : Record 6085593;DocumentNo@1000000001 : Code[20];TemplateNo@1000000002 : Code[20]);
    VAR
      LineIdentField@1000000003 : Record 6085580;
      LineIdentFieldFound@1000000004 : Boolean;
    BEGIN
      WITH LineIdentField DO BEGIN
        SETRANGE("Template No.",TemplateNo);
        SETRANGE(Type,Type::Line);
        SETRANGE(Required,TRUE);
        SETRANGE("Advanced Line Recognition Type","Advanced Line Recognition Type"::Default);
        IF FINDSET THEN
          REPEAT
            IF LineIdentFieldDocumentValue.GET(DocumentNo,TRUE,LineIdentField.Code,1) THEN
              IF (LineIdentFieldDocumentValue."Template No." = TemplateNo) AND
                 (LineIdentFieldDocumentValue.Type = LineIdentFieldDocumentValue.Type::Line)
              THEN
                LineIdentFieldFound := TRUE;
          UNTIL (NEXT = 0) OR LineIdentFieldFound;
      END;

      IF NOT LineIdentFieldFound THEN
        ERROR(Text020);
    END;

    LOCAL PROCEDURE GetSelectedFieldValue@1000000007(VAR SelectedFieldDocumentValue@1000000000 : Record 6085593;SelectedField@1000000001 : Record 6085580;DocumentNo@1000000002 : Code[20];TemplateNo@1000000003 : Code[20]);
    BEGIN
      SelectedFieldDocumentValue.SETRANGE("Document No.",DocumentNo);
      SelectedFieldDocumentValue.SETRANGE("Is Value",TRUE);
      SelectedFieldDocumentValue.SETRANGE(Code,SelectedField.Code);
      SelectedFieldDocumentValue.SETRANGE("Line No.",0,1);
      SelectedFieldDocumentValue.SETRANGE(Type,SelectedFieldDocumentValue.Type::Line);
      SelectedFieldDocumentValue.SETRANGE("Is Valid",TRUE);
      SelectedFieldDocumentValue.SETRANGE("Template No.",TemplateNo);
      IF NOT SelectedFieldDocumentValue.FINDFIRST THEN
        ERROR(Text004,SelectedField."Field Name",SelectedField.Code);  //value is mandatory
    END;

    LOCAL PROCEDURE UpdateExecutionSequence@1000000001(VAR LinkedField@1000000000 : Record 6085580;PreviousFieldCode@1000000001 : Code[20]);
    VAR
      CurrField@1000000002 : Record 6085580;
      PrevField@1000000003 : Record 6085580;
      SortField@1000000004 : Record 6085580;
    BEGIN
      IF NOT PrevField.GET(LinkedField."Template No.",PrevField.Type::Line,PreviousFieldCode) THEN
        EXIT;

      IF LinkedField.Sorting <= PrevField.Sorting  THEN BEGIN
        SortField.SETRANGE("Template No.",LinkedField."Template No.");
        SortField.SETRANGE(Type,SortField.Type::Line);
        SortField.SETFILTER(Code,'<>%1',LinkedField.Code);
        SortField.SETFILTER(Sorting, '>=%1',LinkedField.Sorting  + 1);
        IF SortField.FINDSET THEN
          REPEAT
            SortField.Sorting := SortField.Sorting  + 1;
            SortField.MODIFY;
          UNTIL SortField.NEXT = 0;
        LinkedField.Sorting := LinkedField.Sorting + 1;
      END;
    END;

    BEGIN
    {
      -----------------------------------------------------
      CKL Software GmbH

      Ver Date     Usr Change
      -----------------------------------------------------
      001 20180301 SRA Initial Commit
      002 20180325 SRA New functions for Caption in Line and Offset Line
                       Renamed of existing functions
      004 20180717 SRA Redesign, new funtionality
      006 20190219 SRA New Function to reset a selectable field to standard capturing
      007 20190613 SRA New Object number due to conflicts with the default training objects
      -----------------------------------------------------
    }
    END.
  }
}

