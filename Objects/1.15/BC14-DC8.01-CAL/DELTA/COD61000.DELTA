OBJECT Codeunit 61000 Adv. Line Recognition Mgt.
{
  OBJECT-PROPERTIES
  {
    Date=20220105D;
    Time=225506.917T;
    Modified=true;
    Version List=ALR1.15;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      FieldSetupCanceled@1000000000 : TextConst 'DAN=Feltops‘tning afbrudt fordi intet felt blev valgt!;DEU=Feldeinrichtung abgebrochen, da kein Feld ausgew„hlt wurde!;ENU=Field setup aborted because no field was selected!;FRA=La configuration du champ a ‚t‚ interrompue car aucun champ n''a ‚t‚ s‚lectionn‚!
    ;NLD=Veld instellen afgebroken, omdat er geen veld is geselecteerd!';
      FieldIsLinkedToSourceField@1000000001 : TextConst 'DAN=Feltet "%1" er nu forbundet med feltet "%2".;DEU=Das Feld "%1" ist nun mit dem Feld "%2" verknpft.;ENU=The field "%1" is now linked to field "%2".;FRA=Le champ "%1" est maintenant li‚ au champ "%2 ".;NLD=Het veld "%1" is nu gekoppeld aan het veld "%2".';
      MissingSourceFieldValue@1000000002 : TextConst 'DAN=V‘rdien for kildefeltet %1 i linje %2 mangler! Tr‘n venligst denne v‘rdi f›rst!;DEU=Der Wert fr Herkunftsfeld %1 in Zeile %2 fehlt! Bitte trainieren Sie zuerst diesen Wert an!;ENU=The value for source field %1 in line %2 is missing! Please train this value first!;FRA=La valeur du champ source %1 … la ligne %2 est manquante! Veuillez d''abord entraŒner cette valeur!;NLD=De waarde voor het bronveld %1 in lijn %2 ontbreekt! train eerst deze waarde!';
      MissingFieldExampleValue@1000000003 : TextConst 'DAN=V‘rdien for %1 (%2) i r‘kke %3 mangler! Tr‘n venligst denne v‘rdi f›rst!;DEU=Der Wert fr fr %1 (%2) Zeile %3 fehlt! Bitte trainieren Sie zuerst diesen Wert an!;ENU=The value for for %1 (%2) row %3 is missing! Please train this value first!;FRA=La valeur de %1 (%2) … la ligne %3 est manquante! Veuillez d''abord entraŒner cette valeur!;NLD=De waarde voor %1 (%2) in rij %3 ontbreekt! train eerst deze waarde!';
      SelectOffsetSourceFieldFirst@1000000004 : TextConst 'DAN=V‘lg f›rst kildefeltet der skal v‘re grundlag for positionen hvor v‘rdien skal findes.;DEU="Bitte w„hlen Sie zuerst das Herkunftsfeld aus, an Hand dessen Position der Wert ermittelt werden soll. ";ENU="Please select first the source field on the basis of which position the value should be found. ";FRA=Veuillez d''abord s‚lectionner le champ source sur base duquel la position de la valeur doit ˆtre trouv‚e.;NLD=Selecteer eerst het bronveld op basis van welke positie de waarde moet worden gevonden.';
      CodeunitDoesNotExist@1000000005 : TextConst 'DAN=Codeunit %1 findes ikke! Er objektet omd›bt?;DEU=Die Standard Codeunit %1 existiert nicht! Wurde das Object ggf. umbenannt?;ENU=Codeunit %1 does not exist! Has the object been renamed?;FRA=Le Codeunit %1 n''existe pas ! L''objet a-t-il ‚t‚ renomm‚?;NLD=Codeunit %1 bestaat niet! Is het object hernoemd?';
      TrainCaptionFirstForField@1113400001 : TextConst 'DAN=Fejl! Feltet %1 er ikke forbundet med en overskrift! Mark‚r venligst overskriften f›rst.;DEU=Fehler! Fr Feld %1 wurde noch keine Caption hinterlegt! Bitte pflegen Sie die Caption zuerst.;ENU=Error! For the field %1 hasn|t been trained a caption! Please train the caption first.;FRA=Erreur! Pour le champ %1 une l‚gende n''a pas ‚t‚ entraŒn‚e ! Veuillez d''abord entraŒner la l‚gende.;NLD=Fout! Voor het veld %1 is het onderschrift getraind! Train het bijschrift eerst.';
      SelectTheOffsetField@1113400006 : TextConst 'DAN=V‘lg feltet som skal sammenk‘des med kildefeltet "%1".;DEU=W„hlen Sie nun das Feld aus, welches Sie mit dem Herkunftsfeld "%1" verknpfen m”chten.;ENU=Please choose the field, which should be linked with the source field "%1".;FRA=Veuillez choisir le champ qui doit ˆtre li‚ au champ source "%1".;NLD=Kies het veld, welke gekoppeld moet zijn met het bronveld "%1".';
      ErrorDuringFieldSetup@1113400007 : TextConst 'DAN=Fejl under indstilling af felt.;DEU=Fehler bei Feldeinrichtung.;ENU=Error during field setup.;FRA=Erreur lors de la configuration du champ.;NLD=Fout tijdens het instellen van het veld.';
      SelectFieldForColumnHeaderSearch@1113400008 : TextConst 'DAN=V‘lg det felt der skal findes af kolonneoverskriften.;DEU=Bitte w„hlen Sie das Feld aus, welches ber die šberschrift gefunden werden soll.;ENU=Select the field that should be found by the column heading.;FRA=S‚lectionnez le champ qui doit ˆtre trouv‚ par l''en-tˆte de colonne.;NLD=Selecteer het veld dat gevonden moet worden bij de koptekst van de kolom.';
      SelectFieldForCaptionSearch@1113400009 : TextConst 'DAN=V‘lg feltet hvis v‘rdi skal findes ved hj‘lp af overskriften i den aktuelle linje.;DEU="Bitte w„hlen Sie das Feld aus, welches innerhalb der Position mit Hilfe des Suchbegriffs gefunden werden soll. ";ENU=Choose the field whose value should be found by the caption in the current line.;FRA=Choisir le champ dont la valeur doit ˆtre trouv‚e par la l‚gende de la ligne actuelle.;NLD=Kies het veld waarvan de waarde in de huidige regel moet gevonden worden op basis van de koptekst.';
      FieldIsCapturedByColumnHeading@1160040001 : TextConst 'DAN=Feltet "%1" bliver nu fundet ved hj‘lp af kolonneoverskriften "%2".;DEU="Das Feld ""%1"" wird nun innerhalb der Position ber die Spaltenberschrift ""%2"" gesucht. ";ENU=The field "%1" will now be searched via the column heading "%2".;FRA=Le champ "%1" sera maintenant recherch‚ via l''en-tˆte de colonne "%2".;NLD=Het veld "%1" zal nu worden doorzocht via de kolom koptekst "%2".';
      FieldIsCapturedByCaption@1113400010 : TextConst 'DAN=V‘rdien i feltet "%1" bliver nu fundet ved hj‘lp af kolonneoverskriften i den aktuelle linje.;DEU="Das Feld ""%1"" wird nun innerhalb der Position ber den Suchtext/Caption gesucht. ";ENU=The value of field "%1" will now been searched via the caption in the current line.;FRA=La valeur du champ "%1" sera maintenant recherch‚e via la l‚gende dans la ligne actuelle.;NLD=De waarde van veld "%1" zal nu gezocht worden via het bijschrift in de huidige regel.';
      NoRequiredFieldFound@1000000006 : TextConst 'DAN=Intet obligatorisk felt med indstillingen "P†kr‘vet" blev fundet i linje %1! Konfigurer et obligatorisk felt f›rst.;DEU=Es wurde kein Zeilenfeld mit der aktivierten Option "Erforderlich" gefunden! Konfigurieren Sie zuerst ein Pflichtfeld.;ENU=No mandatory field with the option "Required" was found in line %1! Configure a mandatory field first.;FRA=Aucun champ obligatoire avec l''option "Requis" n''a ‚t‚ trouv‚ … la ligne %1! Configurez d''abord un champ obligatoire.;NLD=Er is geen verplicht veld met de optie "Vereist" gevonden in regel %1! Configureer eerst een verplicht veld.';
      ALRVersionNoText@1160040000 : TextConst 'DAN=ALR%1 (%2 Build %3);DEU=ALR%1 (%2 Build %3);ENU=ALR%1 (%2 Build %3);FRA=ALR%1 (%2 Build %3);NLD=ALR%1 (%2 Build %3)';
      NoALRFieldsForReset@1160040002 : TextConst 'DAN=Der er ikke konfigureret felter til avanceret linjegenkendelse, der kan nulstilles.;DEU=Es gibt keine Felder mit aktivierter erweiterter Zeilenerkennung, die zurckgesetzt werden k”nnen.'';ENU=There are not fields configured for advanced line recognition, that can be reset.;FRA=Il n''y a pas de champs configur‚s pour la reconnaissance de ligne avanc‚e, qui peuvent ˆtre r‚initialis‚s.;NLD=Er zijn geen velden, geconfigureerd voor geavanceerde lijnherkenning, gevonden die kunnen worden gereset.';
      YouAreUsingALRVersion@1000 : TextConst 'DEU=You are using version advanced line recognition version: %1;ENU=You are using version advanced line recognition version: %1';

    [LineStart(2)]
    PROCEDURE SetToAnchorLinkedField@1000000010(VAR TempDocumentLine@1000000008 : Record "CDC Temp. Document Line");
    VAR
      AnchorField@1000000007 : Record "CDC Template Field";
      LinkedField@1000000005 : Record "CDC Template Field";
      AnchorFieldDocumentValue@1113400000 : Record "CDC Document Value";
      LinkedFieldDocumentValue@1000000001 : Record "CDC Document Value";
      FieldsLinked@1000000000 : Integer;
    BEGIN
      // Get the anchor field that defines the position
      Message(SelectOffsetSourceFieldFirst);
      if not SelectField(AnchorField,TempDocumentLine."Template No.",'',false) then
        exit;

      // Get document value of the anchor field => is mandatory
      AnchorFieldDocumentValue.SetRange("Document No.",TempDocumentLine."Document No.");
      AnchorFieldDocumentValue.SetRange("Is Value",true);
      AnchorFieldDocumentValue.SetRange(Code,AnchorField.Code);
      //AnchorFieldDocumentValue.SETRANGE("Line No.",1);
      AnchorFieldDocumentValue.SetRange("Line No.",TempDocumentLine."Line No.");
      AnchorFieldDocumentValue.SetRange(Type,AnchorFieldDocumentValue.Type::Line);
      AnchorFieldDocumentValue.SetRange("Is Valid",true);
      AnchorFieldDocumentValue.SetRange("Template No.",TempDocumentLine."Template No.");
      if not AnchorFieldDocumentValue.FindFirst then
        Error(MissingSourceFieldValue,AnchorField.Code,TempDocumentLine."Line No.");

      // Select the field that should be linked with anchor field
      Message(SelectTheOffsetField, AnchorField."Field Name");
      if not SelectField(LinkedField,TempDocumentLine."Template No.",AnchorField.Code,false) then
        Error(FieldSetupCanceled);

      // Link the selected field to anchor field
      // Find the value of the selected field
      LinkedFieldDocumentValue.SetRange("Document No.",TempDocumentLine."Document No.");
      LinkedFieldDocumentValue.SetRange("Is Value",true);
      LinkedFieldDocumentValue.SetRange(Code,LinkedField.Code);
      //LinkedFieldDocumentValue.SETRANGE("Line No.",1);
      LinkedFieldDocumentValue.SetRange("Line No.",TempDocumentLine."Line No.");
      LinkedFieldDocumentValue.SetRange(Type,LinkedFieldDocumentValue.Type::Line);
      LinkedFieldDocumentValue.SetRange("Is Valid",true);
      LinkedFieldDocumentValue.SetRange("Template No.",TempDocumentLine."Template No.");
      if not LinkedFieldDocumentValue.FindFirst then
        Error(MissingFieldExampleValue,LinkedField."Field Name",LinkedField.Code);  //value is mandatory

      ResetField(LinkedField);

      // Calculate and save the offset values at the linked field
      LinkedField."Offset Top" := LinkedFieldDocumentValue.Top - AnchorFieldDocumentValue.Top;
      LinkedField."Offset Left" := LinkedFieldDocumentValue.Left - AnchorFieldDocumentValue.Left;
      LinkedField."Offset Bottom" := LinkedFieldDocumentValue.Bottom - LinkedFieldDocumentValue.Top;
      LinkedField."Offset Right" := LinkedFieldDocumentValue.Right - LinkedFieldDocumentValue.Left;
      LinkedField."Advanced Line Recognition Type" := LinkedField."Advanced Line Recognition Type"::LinkedToAnchorField;
      LinkedField."Linked Field" := AnchorFieldDocumentValue.Code;

      UpdateExecutionSequence(LinkedField,LinkedField."Linked Field");

      if LinkedField.Modify(true) then begin
        SetTemplateToALRProcessing(TempDocumentLine."Template No.");
        Message(FieldIsLinkedToSourceField,LinkedField."Field Name",AnchorField."Field Name");
      end else
        Message(ErrorDuringFieldSetup);
    END;

    [LineStart(56)]
    PROCEDURE SetToFieldSearchWithColumnHeding@1113400003(VAR TempDocumentLine@1113400002 : Record "CDC Temp. Document Line");
    VAR
      LineIdentFieldDocumentValue@1000000001 : Record "CDC Document Value";
      SelectedField@1113400001 : Record "CDC Template Field";
      SelectedFieldDocumentValue@1000000004 : Record "CDC Document Value";
      CDCTemplateFieldCaption@1000000000 : Record "CDC Template Field Caption";
    BEGIN
      // Find field value of a normal position field
      GetLineIdentifierValue(TempDocumentLine,LineIdentFieldDocumentValue);

      // Select field
      Message(SelectFieldForColumnHeaderSearch);
      if not SelectField(SelectedField,TempDocumentLine."Template No.",'',false) then
        Error(FieldSetupCanceled);

      // Check that the selected field has at least one caption
      CDCTemplateFieldCaption.SetRange("Template No.",SelectedField."Template No.");
      CDCTemplateFieldCaption.SetRange(Code,SelectedField.Code);
      CDCTemplateFieldCaption.SetRange(Type,CDCTemplateFieldCaption.Type::Line);
      if not CDCTemplateFieldCaption.FindFirst then
        Error(TrainCaptionFirstForField, SelectedField.Code);

      // Find the value of the selected field
      GetSelectedFieldValue(TempDocumentLine, SelectedFieldDocumentValue,SelectedField);

      ResetField(SelectedField);

      // Setup field for column heading search
      SelectedField."Advanced Line Recognition Type" := SelectedField."Advanced Line Recognition Type"::FindFieldByColumnHeading;

      if SelectedFieldDocumentValue.Top < LineIdentFieldDocumentValue.Top then
        SelectedField."Field value position" := SelectedField."Field value position"::AboveStandardLine
      else
        SelectedField."Field value position" := SelectedField."Field value position"::BelowStandardLine;

      if SelectedField.Modify(true) then begin
        SetTemplateToALRProcessing(TempDocumentLine."Template No.");
        Message(FieldIsCapturedByColumnHeading, SelectedField."Field Name",CDCTemplateFieldCaption.Caption);
      end else
        Message(ErrorDuringFieldSetup);
    END;

    [LineStart(91)]
    PROCEDURE SetToFieldSearchWithCaption@1113400000(VAR TempDocumentLine@1113400000 : Record "CDC Temp. Document Line");
    VAR
      LineIdentFieldDocumentValue@1000000001 : Record "CDC Document Value";
      SelectedField@1113400001 : Record "CDC Template Field";
      SelectedFieldDocumentValue@1000000002 : Record "CDC Document Value";
      CDCTemplateFieldCaption@1000000000 : Record "CDC Template Field Caption";
      DocumentPage@1000000004 : Record "CDC Document Page";
      CaptureEngine@1000000003 : Codeunit "CDC Capture Engine";
    BEGIN
      // Find field value of a normal position field
      GetLineIdentifierValue(TempDocumentLine,LineIdentFieldDocumentValue);

      // Select field
      Message(SelectFieldForCaptionSearch);
      if not SelectField(SelectedField,TempDocumentLine."Template No.",'',false) then
        Error(FieldSetupCanceled);

      // Check that the selected field has at least one caption
      CDCTemplateFieldCaption.SetRange("Template No.",SelectedField."Template No.");
      CDCTemplateFieldCaption.SetRange(Code,SelectedField.Code);
      CDCTemplateFieldCaption.SetRange(Type,CDCTemplateFieldCaption.Type::Line);
      if not CDCTemplateFieldCaption.FindFirst then
        Error(TrainCaptionFirstForField,SelectedField.Code);

      // Find the value of the selected field
      GetSelectedFieldValue(TempDocumentLine, SelectedFieldDocumentValue,SelectedField);

      ResetField(SelectedField);

      if (SelectedFieldDocumentValue.Right - SelectedFieldDocumentValue.Left) > 0 then begin
        DocumentPage.Get(TempDocumentLine."Document No.",TempDocumentLine."Page No.");
        SelectedField."ALR Typical Value Field Width" := Round((SelectedFieldDocumentValue.Right - SelectedFieldDocumentValue.Left)
                                                               / CaptureEngine.GetDPIFactor(150,DocumentPage."TIFF Image Resolution"),1);
      end;

      if (SelectedFieldDocumentValue.Top <> 0) and (CDCTemplateFieldCaption.Top <> 0) and (SelectedFieldDocumentValue.Left <> 0) and (CDCTemplateFieldCaption.Left <> 0) then begin
          SelectedField."ALR Value Caption Offset X" := SelectedFieldDocumentValue.Left - CDCTemplateFieldCaption.Left;
          SelectedField."ALR Value Caption Offset Y" := SelectedFieldDocumentValue.Top - CDCTemplateFieldCaption.Top;
      end;

      SelectedField."Caption Mandatory" := true;

      // Setup field for field search by caption
      SelectedField."Advanced Line Recognition Type" := SelectedField."Advanced Line Recognition Type"::FindFieldByCaptionInPosition;

      if SelectedFieldDocumentValue.Top < LineIdentFieldDocumentValue.Top then
        SelectedField."Field value position" := SelectedField."Field value position"::AboveStandardLine
      else
        SelectedField."Field value position" := SelectedField."Field value position"::BelowStandardLine;

      if SelectedField.Modify(true) then begin;
        SetTemplateToALRProcessing(TempDocumentLine."Template No.");
        Message(FieldIsCapturedByCaption,SelectedField."Field Name");
      end else
        Message(ErrorDuringFieldSetup);
    END;

    [LineStart(139)]
    PROCEDURE ResetFieldFromMenu@1(VAR TempDocumentLine@1001 : Record "CDC Temp. Document Line");
    VAR
      FieldToReset@1000 : Record "CDC Template Field";
    BEGIN
      if SelectField(FieldToReset,TempDocumentLine."Template No.",'',true) then begin
        ResetField(FieldToReset);
        FieldToReset.Modify(true);
      end;
    END;

    [LineStart(145)]
    LOCAL PROCEDURE ResetField@1000000004(VAR TemplateField@1000000000 : Record "CDC Template Field");
    BEGIN
      // Reset the current field to default values
      TemplateField."Search for Value" := false;
      TemplateField.Required := false;
      Clear(TemplateField."Advanced Line Recognition Type");
      Clear(TemplateField."Linked Field");
      Clear(TemplateField."Offset Top");
      Clear(TemplateField."Offset Bottom");
      Clear(TemplateField."Offset Left");
      Clear(TemplateField."Offset Right");
      Clear(TemplateField."Field value position");
      Clear(TemplateField."Field value search direction");
      Clear(TemplateField.Sorting);
      Clear(TemplateField."Replacement Field");
      Clear(TemplateField."Replacement Field Type");
      Clear(TemplateField."Copy Value from Previous Value");
      Clear(TemplateField."ALR Typical Value Field Width");
      Clear(TemplateField."Typical Field Height");
      Clear(TemplateField."Caption Mandatory");
      Clear(TemplateField."ALR Value Caption Offset X");
      Clear(TemplateField."ALR Value Caption Offset Y");
    END;

    [LineStart(167)]
    LOCAL PROCEDURE SelectField@1113400002(VAR TemplateField@1000000000 : Record "CDC Template Field";TemplateNo@1113400001 : Code[20];ExcludedFieldsFilter@1113400000 : Text[250];ALROnlyFields@1160040000 : Boolean) : Boolean;
    VAR
      lTemplateFieldList@1000000003 : Page "CDC Template Field List";
    BEGIN
      TemplateField.SetRange("Template No.",TemplateNo);
      TemplateField.SetRange(Type,TemplateField.Type::Line);

      if ExcludedFieldsFilter <> '' then
        TemplateField.SetFilter(Code,'<>%1',ExcludedFieldsFilter);

      if ALROnlyFields then begin
        TemplateField.SetFilter("Advanced Line Recognition Type",'<>%1',TemplateField."Advanced Line Recognition Type"::Default);
        if TemplateField.IsEmpty then
          Error(NoALRFieldsForReset);
      end;

      lTemplateFieldList.SetTableView(TemplateField);
      lTemplateFieldList.LookupMode(true);
      if lTemplateFieldList.RunModal = ACTION::LookupOK then begin
        lTemplateFieldList.GetRecord(TemplateField);
        exit(true);
      end;
    END;

    [LineStart(187)]
    PROCEDURE SetTemplateToALRProcessing@1113400005(TemplateNo@1113400000 : Code[20]);
    VAR
      lTemplate@1113400001 : Record "CDC Template";
    BEGIN
      // Change Codeunit ID to the advanced line recognition codeunit on template
      if lTemplate.Get(TemplateNo) then begin
        lTemplate.Validate("Codeunit ID: Line Capture",GetAdvLineRecCodeunit());
        lTemplate.Modify(true);
      end;
    END;

    [LineStart(194)]
    LOCAL PROCEDURE GetAdvLineRecCodeunit@1000000000() : Integer;
    VAR
      Object@1000000000 : Record Object;
      AllObjWithCaption@1160040001 : Record AllObjWithCaption;
      ObjectList@1160040000 : Page "CDC Objects 2";
    BEGIN
      Object.SetRange(Type,Object.Type::Codeunit);
      Object.SetRange(Name,'Adv. Purch/Sales Line Capture');
      if not Object.IsEmpty then begin
        if Object.FindSet then begin
          exit(Object.ID);
        end;
      end else begin
        ObjectList.LookupMode(true);
        if ObjectList.RunModal = ACTION::LookupOK then begin
          ObjectList.GetRecord(AllObjWithCaption);
          exit(AllObjWithCaption."Object ID");
        end;

        Error(CodeunitDoesNotExist,'Adv. Purch/Sales Line Capture');
      end;
    END;

    [LineStart(211)]
    LOCAL PROCEDURE GetLineIdentifierValue@1000000002(VAR TempDocumentLine@1160040000 : Record "CDC Temp. Document Line";VAR LineIdentFieldDocumentValue@1000000000 : Record "CDC Document Value");
    VAR
      LineIdentField@1000000003 : Record "CDC Template Field";
      LineIdentFieldFound@1000000004 : Boolean;
    BEGIN
      with LineIdentField do begin
        SetRange("Template No.",TempDocumentLine."Template No.");
        SetRange(Type,Type::Line);
        SetRange(Required,true);
        SetRange("Advanced Line Recognition Type","Advanced Line Recognition Type"::Default);
        if FindSet then
          repeat
            //IF LineIdentFieldDocumentValue.GET(TempDocumentLine."Document No.",TRUE,LineIdentField.Code,1) THEN
            if LineIdentFieldDocumentValue.Get(TempDocumentLine."Document No.",true,LineIdentField.Code,TempDocumentLine."Line No.") then
              if (LineIdentFieldDocumentValue."Template No." = TempDocumentLine."Template No.") and
                 (LineIdentFieldDocumentValue.Type = LineIdentFieldDocumentValue.Type::Line)
              then
                LineIdentFieldFound := true;
          until (Next = 0) or LineIdentFieldFound;
      end;

      if not LineIdentFieldFound then
        Error(NoRequiredFieldFound);
    END;

    [LineStart(231)]
    LOCAL PROCEDURE GetSelectedFieldValue@1000000007(VAR TempDocumentLine@1160040000 : Record "CDC Temp. Document Line";VAR SelectedFieldDocumentValue@1000000000 : Record "CDC Document Value";SelectedField@1000000001 : Record "CDC Template Field");
    BEGIN
      SelectedFieldDocumentValue.SetRange("Document No.",TempDocumentLine."Document No.");
      SelectedFieldDocumentValue.SetRange("Is Value",true);
      SelectedFieldDocumentValue.SetRange(Code,SelectedField.Code);
      //SelectedFieldDocumentValue.SETRANGE("Line No.",0,1);
      SelectedFieldDocumentValue.SetRange("Line No.",TempDocumentLine."Line No.");
      SelectedFieldDocumentValue.SetRange(Type,SelectedFieldDocumentValue.Type::Line);
      SelectedFieldDocumentValue.SetRange("Is Valid",true);
      SelectedFieldDocumentValue.SetRange("Template No.",TempDocumentLine."Template No.");
      if not SelectedFieldDocumentValue.FindFirst then
        Error(MissingFieldExampleValue,SelectedField."Field Name",SelectedField.Code);  //value is mandatory
    END;

    [LineStart(243)]
    LOCAL PROCEDURE UpdateExecutionSequence@1000000001(VAR LinkedField@1000000000 : Record "CDC Template Field";PreviousFieldCode@1000000001 : Code[20]);
    VAR
      CurrField@1000000002 : Record "CDC Template Field";
      PrevField@1000000003 : Record "CDC Template Field";
      SortField@1000000004 : Record "CDC Template Field";
    BEGIN
      if not PrevField.Get(LinkedField."Template No.",PrevField.Type::Line,PreviousFieldCode) then
        exit;

      if LinkedField.Sorting <= PrevField.Sorting  then begin
        SortField.SetRange("Template No.",LinkedField."Template No.");
        SortField.SetRange(Type,SortField.Type::Line);
        SortField.SetFilter(Code,'<>%1',LinkedField.Code);
        SortField.SetFilter(Sorting, '>=%1',LinkedField.Sorting  + 1);
        if SortField.FindSet then
          repeat
            SortField.Sorting := SortField.Sorting  + 1;
            SortField.Modify;
          until SortField.Next = 0;
        LinkedField.Sorting := LinkedField.Sorting + 1;
      end;
    END;

    [LineStart(260)]
    PROCEDURE ShowVersionNo@1160040000() : Text;
    VAR
      VersionTriggers@1160040000 : Codeunit "Version Triggers";
      ApplicationVersion@1160040001 : Text;
      ApplicationBuild@1160040002 : Text;
    BEGIN
      //EXIT(14) //=> for older NAV/BC Versions without the version triggers
      VersionTriggers.GetApplicationVersion(ApplicationVersion);
      VersionTriggers.GetApplicationBuild(ApplicationBuild);

      Message(StrSubstNo(ALRVersionNoText,'15',ApplicationVersion,ApplicationBuild));
    END;

    BEGIN
    END.
  }
}

